#!/usr/bin/env perl
use v5.18;
use warnings;

use PPI;
use Getopt::Long;
use JSON qw(to_json);
use Elastijk;

sub search_p5iq_index {
    my ($query_string, $size) = @_;
    $size //= 10;

    my $ppi_doc = PPI::Document->new( \$query_string );
    my @tokens = grep { $_->significant } $ppi_doc->tokens;

    my $es_query = {
        bool => {
            must   => [ (map { +{ term => { "token_class" => $_->class } } } @tokens) ],
            should => [ (map { +{ match => { token_content => $_->content } } } @tokens) ]
        }
    };

    my $es = Elastijk->new( host => "localhost", port => "9200", index => "p5iq" );
    my ($status, $res) = $es->search(
        index => "p5iq",
        type  => "ppi_statement",
        body  => {
            query => $es_query,
            size  => $size,
        }
    );
    if ($status eq '200') {
        for (@{ $res->{hits}{hits} }) {
            my @loc = split("\0", $_->{_source}{location});
            say join(":", $loc[0], $loc[1]) . "\n" . $_->{_source}{content} . "\n";
        }
    } else {
        say "Query Error: " . to_json($res);
    }
}

GetOptions(
    "s|size=i" => \(my $size)
);
my $query = shift(@ARGV) or die "query required";

search_p5iq_index($query, $size);
