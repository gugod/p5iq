#!/usr/bin/env perl

use v5.18;

use List::MoreUtils qw(any uniq);

use PPI;
use PPIx::LineToSub;
use Getopt::Long;

my %args;
GetOptions(
    \%args,
    "variable=s",
    "file=s",
);

unless (defined($args{variable}) && defined($args{file})) {
    die "Usage: $0 --variable \$foo --file lib/Foo/Bar.pm\n";
}

unless (-f $args{file}) {
    die "File do not exist: $args{file}";
}

my %freqtab;

my $wanted_var_name = $args{variable};
my $p5_doc = PPI::Document->new($args{file});
$p5_doc->index_locations;
$p5_doc->index_line_to_sub;

my $nodes = $p5_doc->find(
    sub {
        my $el = $_[1];
        return ($el->isa("PPI::Token::Symbol") && $el->content eq $wanted_var_name);
    }
) || [];

for my $n (@$nodes) {
    my $ln = $n->line_number;
    $freqtab{line}{$ln}++;

    for (["prev_operator", $n->sprevious_sibling], ["next_operator", $n->snext_sibling]) {
        my ($agg, $m) = @$_;
        next unless $m && $m->isa('PPI::Token::Operator');
        $freqtab{$agg}{"$m"}++;
    }
}

my %operator_type = (
    (map { $_ => "string"    } ('.', 'eq', 'ne', 'cmp', '~~', '=~', '!~', 'le', 'ge', 'lt', 'gt')),
    (map { $_ => "numerical" } ('++', '--', '**', '+', '-', '*', '/', '<', '>', '<=', '>=', '==', '!=', '<=>', '+=', '-=', '*=', '/=')),
    (map { $_ => "reference" } ('->')),
);

for my $a (qw(prev_operator next_operator)) {
    for my $op (keys %{$freqtab{$a}}) {
        my $type = $operator_type{$op} or next;
        $freqtab{"${type}_operator"}{$op} += $freqtab{$a}{$op};
    }
}

for my $ln (keys %{$freqtab{line}}) {
    my $sub_name = $p5_doc->line_to_sub($ln);
    $freqtab{subroutine}{$sub_name}++;
}

delete($freqtab{line});

say "\n# About this variable: $wanted_var_name";
for my $k (sort keys %freqtab) {
    say "\n$k frequency";
    my $sum = 0;
    for my $k2 (sort { $freqtab{$k}{$b} <=> $freqtab{$k}{$a} } keys %{$freqtab{$k}}) {
        printf "  %6d - %s\n", $freqtab{$k}{$k2}, $k2;
        $sum += $freqtab{$k}{$k2};
    }
    printf "  %6d - %s\n", $sum, "(Total)";
}
say "";
